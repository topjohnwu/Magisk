apply plugin: 'com.android.application'

def configProps = new Properties()
configProps.load(new FileInputStream(rootProject.file('config.prop')))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.topjohnwu.magisk"
        minSdkVersion 21
        targetSdkVersion rootProject.ext.compileSdkVersion
    }

    signingConfigs {
        config {
            storeFile rootProject.file('release-key.jks')
            storePassword configProps['keyStorePass']
            keyAlias configProps['keyAlias']
            keyPassword configProps['keyPass']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "mode"

    productFlavors {
        full {
            versionName configProps['appVersion']
            versionCode configProps['appVersionCode'] as Integer
            javaCompileOptions {
                annotationProcessorOptions {
                    argument('butterknife.debuggable', 'false')
                }
            }
        }
        stub {
            versionCode 1
            versionName "stub"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "2g"
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    fullImplementation project(':utils')
    implementation "com.android.support:support-core-utils:${rootProject.ext.supportLibVersion}"
    fullImplementation "com.android.support:preference-v7:${rootProject.ext.supportLibVersion}"
    fullImplementation "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    fullImplementation "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"
    fullImplementation "com.android.support:design:${rootProject.ext.supportLibVersion}"
    fullImplementation 'com.github.topjohnwu:libsu:2.0.1'
    fullImplementation 'com.atlassian.commonmark:commonmark:0.11.0'
    fullImplementation 'org.kamranzafar:jtar:2.3'
    fullImplementation 'com.jakewharton:butterknife:8.8.1'
    fullAnnotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}
